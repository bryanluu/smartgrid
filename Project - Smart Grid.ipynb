{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Data\n",
    "using NamedArrays\n",
    "\n",
    "nodes = [:S, :K, :P, :gamma, :L, :B, :O, :beta, :C, :M, :R, :alpha]\n",
    "us = [:K, :L, :C]\n",
    "producers = [us..., :gamma, :beta, :alpha]\n",
    "consumers = [:S, :K, :P, :L, :B, :O, :C, :M, :R]\n",
    "\n",
    "arcs = [(:K, :P), (:S, :P), (:P, :L), (:P, :gamma), \n",
    "    (:gamma, :B), (:gamma, :L), (:gamma, :R), (:L, :B), (:B, :O),\n",
    "    (:O, :beta), (:beta, :R), (:beta, :C), (:beta, :M),\n",
    "    (:alpha, :gamma), (:alpha, :S), (:alpha, :R)]\n",
    "arcs = [arcs; [(j, i) for (i, j) in arcs]]\n",
    "\n",
    "dist_vec = [1, 2, 6, 3, 4, 5, 7, 5, 3, 4, 2, 2, 3, 5, 5, 5]*100\n",
    "distance = Dict(zip(arcs, [dist_vec; dist_vec]))\n",
    "\n",
    "days = [1:7;]\n",
    "\n",
    "loss = 0.0005\n",
    "\n",
    "matrix = zeros(length(nodes), length(days))\n",
    "demand = NamedArray(matrix, (nodes, days), (\"Consumer\",\"Day\"))\n",
    "demand[:S, :] = [40.8, 100, 230.5, 150.8, 74.2, 97.3, 65.4]\n",
    "demand[:P, :] = [37.8, 30.3, 28.8, 20.5, 37, 45, 29.6]\n",
    "demand[:K, :] = repeat([27.3], length(days));\n",
    "demand[:L, :] = [30.1, 28.8, 27.3, 55.2, 64, 42, 36.6]\n",
    "demand[:C, :] = [30, 35.5, 31.2, 45.5, 50, 43.1, 38.4]\n",
    "demand[:B, :] = [30.1, 31.5, 30.1, 35.1, 34.3, 39, 33.2]\n",
    "demand[:O, :] = [23.7, 28.3, 16.4, 36.7, 20, 17.3, 11.1]\n",
    "demand[:M, :] = [27.3, 21.9, 24.6, 28.9, 26, 30.7, 23.8]\n",
    "demand[:R, :] = [19.2, 20.5, 20.5, 20, 18.4, 21.3, 20.5]\n",
    "\n",
    "matrix = zeros(length(nodes), length(days))\n",
    "supply = NamedArray(matrix, (nodes, days), (\"Producer\",\"Day\"))\n",
    "supply[:alpha, :] = [600, 300, 700, 0, 100, 0, 350]\n",
    "supply[:beta, :] = [500, 200, 900, 0, 50, 0, 300]\n",
    "supply[:L, :] = [150, 200, 150, 305, 200, 30, 500]\n",
    "supply[:K, :] = [200, 150, 100, 600, 150, 40, 200]\n",
    "supply[:C, :] = [150, 230, 180, 120, 40, 20, 140]\n",
    "supply[:gamma, :] .= 10000;\n",
    "\n",
    "# punishment for using gamma\n",
    "µ = [0.01, 0.07, 0.22;];\n",
    "\n",
    "# battery capacities\n",
    "cap = Dict(zip(nodes, zeros(length(nodes))))\n",
    "for c in consumers\n",
    "    cap[c] = 20;\n",
    "end\n",
    "cap[:S] = 100;\n",
    "\n",
    "# battery efficiency\n",
    "eff_in = 0.6;\n",
    "eff_out = 0.8;\n",
    "\n",
    "# cost per kWh lost\n",
    "cost = 1;\n",
    "\n",
    "# dirty power penalty\n",
    "penalty = 10;\n",
    "\n",
    "# price of energy sold to the grid\n",
    "price = 1.5;\n",
    "\n",
    "# cycle prevention coefficient\n",
    "cp = 0.16;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "plot_flow (generic function with 1 method)"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using GraphPlot, LightGraphs, Colors\n",
    "\n",
    "# Plots the graph of the flows\n",
    "# flow is a dictionary of arcs -> flows\n",
    "# Green nodes represents energy-giving nodes, Red nodes represents energy-taking nodes, Gray nodes are neutral nodes.\n",
    "# Green edges means energy flows there\n",
    "# Labels appear at the destination of nodes\n",
    "function plot_flow(flow)\n",
    "    \n",
    "N = length(nodes)\n",
    "E = length(arcs)\n",
    "nodemap = Dict(zip(nodes, 1:N))\n",
    "g = SimpleDiGraph(N)\n",
    "for (i,j) in arcs\n",
    "    add_edge!(g, nodemap[i], nodemap[j])\n",
    "end\n",
    "                                                            \n",
    "inflows = Dict(zip(nodes, [sum(flow[(i,j)] for (i,j) in arcs if j == k) for k in nodes]))\n",
    "outflows = Dict(zip(nodes, [sum(flow[(i,j)] for (i,j) in arcs if i == k) for k in nodes]))\n",
    "                                                                                        \n",
    "# vector of net outflows for each node\n",
    "net_outflows = [outflows[k] - inflows[k] for k in nodes]\n",
    "\n",
    "# color nodes in proportion to their net outflows                                                            \n",
    "l, u = min(net_outflows...), max(net_outflows...) # lower and upper bounds on the colors\n",
    "node_colors = [(c > 0 ? RGBA(0.5*(1-c/u), 0.5*(1+c/u), 0.5*(1-c/u), 1) \n",
    "        : RGBA(0.5*(1+c/l), 0.5*(1-c/l), 0.5*(1-c/l), 1)) for c in net_outflows]\n",
    "\n",
    "# sort arcs by their tuple of numerical node indices\n",
    "arcmap = sort(arcs, by=(n -> (nodemap[n[1]], nodemap[n[2]])))\n",
    "\n",
    "# locations of the nodes, note that y is reversed\n",
    "locs_x = [0, 0.5, 1, 2, 4, 3.5, 4, 2, 1.5, 3, 1, 0]\n",
    "locs_y = -[0, -0.5, 0, 1, 0, 2, 3, 3, 4, 4, 3.5, 2]\n",
    "    \n",
    "# color the edges in proportion to their flow\n",
    "maxflow = max([flow[a] for a in arcs]...)\n",
    "lg = colorant\"lightgray\" # lightgray\n",
    "dominant_flows = Dict(zip(arcs, [max(flow[(i,j)], flow[(j,i)]) for (i,j) in arcs]))\n",
    "edge_colors = [(dominant_flows[a] > 0 ? RGBA(lg.r*(1-dominant_flows[a]/maxflow), \n",
    "        dominant_flows[a]*(1-lg.g)/maxflow + lg.g, \n",
    "        lg.b*(1-dominant_flows[a]/maxflow), 1) : lg) for a in arcmap]\n",
    "    \n",
    "# plot graph\n",
    "gplot(g, nodelabel=[(n in producers ? String(n)*\"*\" : n) for n in nodes], locs_x, locs_y,\n",
    "    edgelabel=[(flow[a] > 0 ? string(round(flow[a],1)) : \"\") for a in arcmap], \n",
    "    arrowlengthfrac=0, edgelabeldistx=0, edgelabeldisty=0,\n",
    "    nodefillc=node_colors, edgestrokec=edge_colors, NODESIZE=0.05)\n",
    "                                                            \n",
    "end\n",
    "                                                        \n",
    "# flow = Dict(zip(arcs, [getvalue(x[a, day]) for a in arcs]))\n",
    "\n",
    "# plot_flow(flow)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 65,
   "metadata": {
    "scrolled": false
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "solve_flows (generic function with 1 method)"
      ]
     },
     "execution_count": 65,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# Model\n",
    "using Clp, JuMP\n",
    "\n",
    "# fill an array of length len using non-repeating elements of A\n",
    "function fillrand(A, len)\n",
    "    B = copy(A)\n",
    "    C = Array{typeof(A[1])}(0)\n",
    "    for i in 1:len\n",
    "        day = rand(B)\n",
    "        deleteat!(B, findin(B, day))\n",
    "        append!(C, day)\n",
    "    end\n",
    "    return C\n",
    "end\n",
    "\n",
    "# --- solve the model with given parameters ---\n",
    "# price: how much sharing energy (per kWh) is rewarded\n",
    "# cost: how much energy losses cost (per kWh)\n",
    "# penalty: how much dirty power is punished (per kWh)\n",
    "# cp: how much the flow capacities sum should be punished (per kWh)\n",
    "# verbose: whether to display large output info\n",
    "# random_demand: whether to generate randomized demand\n",
    "# random_supply: whether to generate randomized supply\n",
    "# seed: random seed to use (default between 1 to 1000000)\n",
    "# random_dist: what type of random distribution to use (:Gaussian or :Uniform)\n",
    "# random_std_p: percent (0 to 1) of the mean the random distribution used for its std\n",
    "# no_wind: either an array of days without wind, or the number of randomly generated no wind days\n",
    "# no_sun: either an array of days without sun, or the number of randomly generated no sun days\n",
    "# cp_step: what to increase cp by during each iteration with cycles\n",
    "# max_iter: maximum number of solves until no cycles\n",
    "function solve_flows(;price=1, cost=1, penalty=10, cp=10, verbose=true, \n",
    "                            random_demand=false, random_supply=false, seed=-1,\n",
    "                            random_dist=:Gaussian, random_std_p=0.3,\n",
    "                            no_wind = [], no_sun = [],\n",
    "                            cp_step = 5, max_iter = 10)\n",
    "# calculate useful set sizes\n",
    "N = length(nodes)\n",
    "T = length(days)\n",
    "E = length(arcs)\n",
    "    \n",
    "if typeof(max_iter) != Int64\n",
    "    println(\"Invalid type for max_iter!\")\n",
    "    return\n",
    "end\n",
    "    \n",
    "if max_iter < 1\n",
    "    println(\"Invalid value for max_iter (must be >= 1): \", max_iter)\n",
    "    return\n",
    "end\n",
    "    \n",
    "if typeof(cp_step) != Int64\n",
    "    println(\"Invalid type for cp_step!\")\n",
    "    return\n",
    "end\n",
    "\n",
    "if cp_step <= 0\n",
    "    println(\"Invalid cp_step (must be > 0): \", cp_step)\n",
    "    return\n",
    "end    \n",
    "    \n",
    "# check if random no wind days\n",
    "random_no_wind = false\n",
    "if typeof(no_wind) == Int64\n",
    "    if !(1 <= no_wind <= length(days))\n",
    "        println(\"Error: \", no_wind, \" is not a valid number of days!\")\n",
    "        println(\"Ignoring no wind days...\")\n",
    "    else\n",
    "        random_no_wind = true\n",
    "    end\n",
    "end\n",
    "    \n",
    "# check if random no sun days\n",
    "random_no_sun = false\n",
    "if typeof(no_sun) == Int64\n",
    "    if !(0 <= no_sun <= length(days))\n",
    "        println(\"Error: \", no_sun, \" is not a valid number of days!\")\n",
    "        println(\"Ignoring no sun days...\")\n",
    "    else\n",
    "        random_no_sun = true\n",
    "    end\n",
    "end\n",
    "    \n",
    "# will the model be randomized?\n",
    "random = random_demand | random_supply | random_no_wind | random_no_sun\n",
    "    \n",
    "# if seed is provided, seed the random number generator (RNG), otherwise, generate it with a seed from 1:1000000\n",
    "if seed >= 0 \n",
    "    srand(seed); # seed the RNG\n",
    "    if !random\n",
    "        println(\"Seed provided but nothing is being randomized! Oops...\")\n",
    "        println(\"Using default values for supply and demand.\")\n",
    "    end\n",
    "else\n",
    "    seed = rand(1:1000000)\n",
    "    if random\n",
    "        println(\"Seed: \", seed)\n",
    "    end\n",
    "    srand(seed);\n",
    "end\n",
    "\n",
    "# generate random demand values if random_demand is true, otherwise set it to default\n",
    "set_demand = NamedArray(zeros(N, T), (nodes, days), (\"Nodes\", \"Day\"))\n",
    "_demand = copy(demand)\n",
    "\n",
    "if random_demand\n",
    "    if random_dist == :Uniform\n",
    "        set_demand[consumers, :] = 60\n",
    "        set_demand[:S, :] = 250\n",
    "        _demand = rand(N,T).*set_demand\n",
    "    elseif random_dist == :Gaussian\n",
    "        set_demand[consumers, :] = 40\n",
    "        set_demand[:S, :] = 150\n",
    "        _demand = max.(0, randn(N,T).*set_demand.*random_std_p + set_demand)\n",
    "    else\n",
    "        println(\"Invalid Random Distribution: \", random_dist)\n",
    "        return\n",
    "    end\n",
    "end\n",
    "    \n",
    "# generate random supply values if random_supply is true, otherwise set it to default\n",
    "set_supply = NamedArray(zeros(N, T), (nodes, days), (\"Nodes\", \"Day\"))\n",
    "_supply = copy(supply)\n",
    "    \n",
    "if random_supply\n",
    "    if random_dist == :Uniform\n",
    "        set_supply[producers, :] = 500\n",
    "        set_supply[us, :] = 300\n",
    "        _supply = rand(N,T).*set_supply\n",
    "    elseif random_dist == :Gaussian\n",
    "        set_supply[producers, :] = 350\n",
    "        set_supply[us, :] = 200\n",
    "        _supply = max.(0, randn(N,T).*set_supply.*random_std_p + set_supply)\n",
    "    else\n",
    "        println(\"Invalid Random Distribution: \", random_dist)\n",
    "        return\n",
    "    end\n",
    "    _supply[:gamma, :] = 10000;\n",
    "end\n",
    "\n",
    "\n",
    "# generate no wind days if specified\n",
    "if random_no_wind\n",
    "    no_wind = fillrand(days, no_wind)\n",
    "    println(\"No wind days: \", no_wind)\n",
    "end\n",
    "    \n",
    "# set no_wind days to have 0 supply from alpha and beta\n",
    "if length(no_wind) > 0\n",
    "    if all([i in days for i in no_wind])\n",
    "        _supply[:alpha, no_wind] = 0\n",
    "        _supply[:beta, no_wind] = 0\n",
    "    else\n",
    "        println(\"Error: \", [i for i in no_wind if !(i in days)], \" not included in days \", days)\n",
    "        println(\"Ignoring no wind days...\")\n",
    "    end\n",
    "end\n",
    "                        \n",
    "# generate no sun days if specified\n",
    "if random_no_sun\n",
    "    no_sun = fillrand(days, no_sun)\n",
    "    println(\"No sun days: \", no_sun)\n",
    "end\n",
    "\n",
    "# set no_sun days to have 0 supply from C, L, K\n",
    "if length(no_sun) > 0\n",
    "    if all([i in days for i in no_sun])\n",
    "        _supply[us, no_sun] = 0\n",
    "    else\n",
    "        println(\"Error: \", [i for i in no_sun if !(i in days)], \" not included in days \", days)\n",
    "        println(\"Ignoring no sun days...\")\n",
    "    end\n",
    "end\n",
    "                                            \n",
    "if random_supply\n",
    "    println(\"Randomized supply: \", _supply)\n",
    "end\n",
    "if random_demand\n",
    "    println(\"Randomized demand: \", _demand)\n",
    "end\n",
    "                                            \n",
    "# calculate b matrix (net supply)\n",
    "b = NamedArray(zeros(N,T), (nodes, days), (\"Nodes\", \"Day\"))\n",
    "for n in nodes\n",
    "    for t in days\n",
    "        b[n,t] = _supply[n,t] - _demand[n,t]\n",
    "    end\n",
    "end\n",
    " \n",
    "# create model\n",
    "m = Model(solver=ClpSolver())\n",
    "\n",
    "@variable(m, x[arcs, days] >= 0) # energy flow from node i to j\n",
    "@variable(m, s[nodes, 1:max(days...)+1] >= 0) # energy stored in battery at node i on day t\n",
    "@variable(m, y[arcs, days] >= 0) # cap for flow along arc (i, j)\n",
    "\n",
    "# # useful expressions\n",
    "@expression(m, losses[a in arcs, t in days], loss*distance[a]*x[a, t]) # loss along arc a on day t\n",
    "@expression(m, energy_transmitted[a in arcs, t in days], x[a, t] - losses[a, t]) # energy transmitted along a on day t\n",
    "@expression(m, flow_in_lossless[k in nodes, t in days], \n",
    "    sum(x[(i,j), t] for (i,j) in arcs if j == k)) # flow into node k without considering loss on day t\n",
    "@expression(m, flow_in[k in nodes, t in days], \n",
    "    sum(energy_transmitted[(i,j), t] for (i,j) in arcs if j == k)) # flow into k on day t\n",
    "@expression(m, flow_out[k in nodes, t in days], \n",
    "    sum(x[(i,j), t] for (i,j) in arcs if i == k)) # flow out of k on day t\n",
    "@expression(m, energy_lost, sum(loss*distance[a]*x[a, t] for a in arcs for t in days) \n",
    "    + (1-eff_in*eff_out)*sum(s[k, t] for k in nodes for t in 1:days[end]+1)) # losses from transport and batteries\n",
    "@expression(m, dirty_power, sum(flow_out[:gamma, t] for t in days)) # power provided by gamma (main grid)\n",
    "@expression(m, net_outflow[k in nodes, t in days], flow_out[k, t] - flow_in[k, t]) # net outflow from node k on day t\n",
    "# leftover power for node k on day t                                                                                                                        \n",
    "@expression(m, leftover[k in nodes, t in days], b[k, t] - net_outflow[k, t] + s[k, t]) \n",
    "# total power generated by producers\n",
    "@expression(m, production, sum(net_outflow[k, t] for k in producers, t in days if k != :gamma))\n",
    "# sum of the flow capacities\n",
    "@expression(m, capsum, sum(y))                                                                                            \n",
    "\n",
    "# power balance for node k on day t                                                                                                                                        \n",
    "@constraint(m, power_balance[k in nodes, t in days], net_outflow[k, t] + s[k, t+1] - eff_out*s[k, t] <= b[k, t])\n",
    "# battery input efficiency constraint                                                                                                                                        \n",
    "@constraint(m, battery_efficiency[k in nodes, t in days], s[k, t+1] <= eff_in*leftover[k, t])\n",
    "# no initial battery power available                                                                                                                                        \n",
    "@constraint(m, starting_battery_savings[k in nodes], s[k, days[1]] == 0)\n",
    "# battery capacity constraint                                                                                                                    \n",
    "@constraint(m, battery_capacity[k in nodes, t in 1:days[end]+1], s[k, t] <= cap[k])\n",
    "# just provide minimum energy needed constraint                                                                                                                                        \n",
    "@constraint(m, sufficiency[k in nodes, t in days], -net_outflow[k, t] <= _demand[k, t] + s[k, t])\n",
    "# flow cap symmetry constraint                                     \n",
    "@constraint(m, symmetry[(i,j) in arcs, t in days], y[(i,j), t] == y[(j,i), t])\n",
    "# flow cap constraint                                                                                                                                        \n",
    "@constraint(m, limit_flow[a in arcs, t in days], x[a,t] <= y[a,t])\n",
    "# cycle breaking constraint\n",
    "@constraint(m, cyclebreaker[(i,j) in arcs, t in days], x[(i,j),t] + x[(j,i),t] == y[(i,j), t])                                                                                                                                        \n",
    "\n",
    "# no-cycle solve iterations                                                                                                                                        \n",
    "cp_iter = 0;\n",
    "cp_val = cp;                                                                                                                                        \n",
    "has_cycles = true;\n",
    "\n",
    "# initialize data variables outside of while-loop                                                                                                                                        \n",
    "flow = flowcaps = lost = storage = revenue = dirt = costs = cyclesum = 0;                                                                                                                                        \n",
    "                                                                                                                                        \n",
    "while (has_cycles & (cp_iter < max_iter))\n",
    "                                                                                                                                            \n",
    "@objective(m, Min, cost*energy_lost + penalty*dirty_power - price*production + cp_val*capsum)\n",
    "                                                                                                                                            \n",
    "status = solve(m)\n",
    " \n",
    "# calculate resulting data                                                                                                                                        \n",
    "flow = NamedArray(zeros(length(arcs), length(days)), (arcs, days), (\"Edges\",\"Day\"))\n",
    "flowcaps = NamedArray(zeros(length(arcs), length(days)), (arcs, days), (\"Edges\",\"Day\"))\n",
    "lost = NamedArray(zeros(length(arcs), length(days)), (arcs, days), (\"Edges\",\"Day\"))                                                                                                            \n",
    "storage = NamedArray(zeros(length(nodes), length(days)), (nodes, days), (\"Nodes\",\"Day\"))\n",
    "revenue = getvalue(price*production)\n",
    "dirt = getvalue(penalty*dirty_power)\n",
    "costs = getvalue(cost*energy_lost)\n",
    "cyclesum = getvalue(cp*capsum)\n",
    "                                                                                                            \n",
    "for t in days\n",
    "    for a in arcs\n",
    "        flow[a,t] = getvalue(x[a,t])\n",
    "        lost[a,t] = getvalue(losses[a,t])\n",
    "        flowcaps[a,t] = getvalue(y[a,t])\n",
    "    end \n",
    "    for n in nodes\n",
    "        storage[n,t] = getvalue(s[n,t])\n",
    "    end\n",
    "end\n",
    "\n",
    "# determines whether cycles are present\n",
    "has_cycles = (sum(flow[(i,j),t]*flow[(j,i),t] for (i,j) in arcs for t in days) > 0)\n",
    "\n",
    "# increase iteration cycle\n",
    "cp_iter += 1;\n",
    "\n",
    "if(verbose)\n",
    "    println(\"--- Iteration: \", cp_iter, \" ---\")\n",
    "    println(\"cp=\", cp_val)\n",
    "else\n",
    "    print(\".\")\n",
    "end\n",
    "\n",
    "if has_cycles\n",
    "    if(verbose)\n",
    "        println(\"Cycles detected.\")\n",
    "    end\n",
    "    cp_val += cp_step\n",
    "end\n",
    "\n",
    "end # end of while loop\n",
    "\n",
    "# verbose output\n",
    "if(verbose)\n",
    "    print(\"\\n\")\n",
    "    if(!has_cycles)\n",
    "        println(\"No-cycle solution found after \", cp_iter, \" iterations. cp=\", cp_val)\n",
    "    end\n",
    "    println(\"------Solution------\")\n",
    "    println(getvalue(x))\n",
    "    println(\"------\")\n",
    "    println(getvalue(s))\n",
    "    println(\"Total energy losses will be \", getvalue(energy_lost), \" kWh.\")\n",
    "    println(\"Total dirty energy used will be \", getvalue(dirty_power), \" kWh.\")\n",
    "    println(\"Revenue: \\$\", revenue)\n",
    "end\n",
    "\n",
    "# construct data dictionary                                                                                                                                                    \n",
    "data = Dict(\"flows\"=>flow, \"storage\"=>storage, \"flowcaps\"=>flowcaps,\n",
    "    \"revenue\"=>revenue, \"losses\"=>lost, \"costs\"=>costs, \"cycles?\"=> has_cycles,\n",
    "    \"dirt\"=>dirt, \"cycleterm\"=>cyclesum);\n",
    "if random_demand\n",
    "    data[\"demand\"] = _demand\n",
    "end\n",
    "if random_supply\n",
    "    data[\"supply\"] = _supply\n",
    "end                                                                                                                                                    \n",
    "if random\n",
    "    data[\"seed\"] = seed\n",
    "end\n",
    "if cp_iter > 1\n",
    "    data[\"cp\"] = cp_val\n",
    "end\n",
    "                                                                                                                                                    \n",
    "return data\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 64,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "No wind days: [1, 7]\n",
      "No sun days: [5, 3, 4]\n",
      "Randomized supply: 12×7 Named Array{Float64,2}\n",
      "Nodes ╲ Day │       1        2        3        4        5        6        7\n",
      "────────────┼──────────────────────────────────────────────────────────────\n",
      ":S          │     0.0      0.0      0.0      0.0      0.0      0.0      0.0\n",
      ":K          │ 28.0056  21.7075      0.0      0.0      0.0   273.45  157.936\n",
      ":P          │     0.0      0.0      0.0      0.0      0.0      0.0      0.0\n",
      ":gamma      │ 10000.0  10000.0  10000.0  10000.0  10000.0  10000.0  10000.0\n",
      ":L          │ 207.126  277.959      0.0      0.0      0.0  13.8369  60.7957\n",
      ":B          │     0.0      0.0      0.0      0.0      0.0      0.0      0.0\n",
      ":O          │     0.0      0.0      0.0      0.0      0.0      0.0      0.0\n",
      ":beta       │     0.0  379.589  404.928  497.594  115.844  366.679      0.0\n",
      ":C          │ 267.617  283.562      0.0      0.0      0.0  51.9477  254.608\n",
      ":M          │     0.0      0.0      0.0      0.0      0.0      0.0      0.0\n",
      ":R          │     0.0      0.0      0.0      0.0      0.0      0.0      0.0\n",
      ":alpha      │     0.0  324.947  119.992  30.7484  205.169  299.994      0.0\n",
      "Randomized demand: 12×7 Named Array{Float64,2}\n",
      "Nodes ╲ Day │       1        2        3        4        5        6        7\n",
      "────────────┼──────────────────────────────────────────────────────────────\n",
      ":S          │ 161.354  136.721  244.806    211.6  131.759  73.2068  142.292\n",
      ":K          │ 4.33887  42.0226   46.711  39.0872  34.9932  31.2439  54.2812\n",
      ":P          │ 59.1016  10.6934  43.9824  12.7035    5.522  1.62948  38.5294\n",
      ":gamma      │     0.0      0.0      0.0      0.0      0.0      0.0      0.0\n",
      ":L          │ 24.0443  50.3312  25.1856  27.8501  8.31506  18.3958  47.1372\n",
      ":B          │ 12.4334  7.66726  12.1372  21.9998  17.4594  42.2911  46.8471\n",
      ":O          │ 30.0697   19.729  16.4453  56.0677  18.5299  34.8146  23.2166\n",
      ":beta       │     0.0      0.0      0.0      0.0      0.0      0.0      0.0\n",
      ":C          │ 14.0239  46.8277  11.4926   23.709  31.1608  49.8183  36.7498\n",
      ":M          │ 18.9125  47.3773  44.5056  51.6161  30.2116  21.2547  25.8196\n",
      ":R          │ 37.1042  12.1587  29.6122  13.4158  48.2418  35.9835  49.6389\n",
      ":alpha      │     0.0      0.0      0.0      0.0      0.0      0.0      0.0\n",
      "..."
     ]
    },
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "\u001b[1m\u001b[33mWARNING: \u001b[39m\u001b[22m\u001b[33muuid4(rng) is deprecated, use Compat.UUIDs.uuid4(rng) instead.\u001b[39m\n",
      "Stacktrace:\n",
      " [1] \u001b[1mdepwarn\u001b[22m\u001b[22m\u001b[1m(\u001b[22m\u001b[22m::String, ::Symbol\u001b[1m)\u001b[22m\u001b[22m at \u001b[1m./deprecated.jl:70\u001b[22m\u001b[22m\n",
      " [2] \u001b[1muuid4\u001b[22m\u001b[22m\u001b[1m(\u001b[22m\u001b[22m::MersenneTwister\u001b[1m)\u001b[22m\u001b[22m at \u001b[1m./deprecated.jl:57\u001b[22m\u001b[22m\n",
      " [3] \u001b[1mmsg_header\u001b[22m\u001b[22m at \u001b[1m/Users/bryan_luu30794/.julia/v0.6/IJulia/src/msg.jl:18\u001b[22m\u001b[22m [inlined]\n",
      " [4] \u001b[1mmsg_pub\u001b[22m\u001b[22m\u001b[1m(\u001b[22m\u001b[22m::IJulia.Msg, ::String, ::Dict{String,String}, ::Dict{String,Any}\u001b[1m)\u001b[22m\u001b[22m at \u001b[1m/Users/bryan_luu30794/.julia/v0.6/IJulia/src/msg.jl:30\u001b[22m\u001b[22m (repeats 2 times)\n",
      " [5] \u001b[1msend_stream\u001b[22m\u001b[22m\u001b[1m(\u001b[22m\u001b[22m::String\u001b[1m)\u001b[22m\u001b[22m at \u001b[1m/Users/bryan_luu30794/.julia/v0.6/IJulia/src/stdio.jl:172\u001b[22m\u001b[22m\n",
      " [6] \u001b[1msend_stdio\u001b[22m\u001b[22m\u001b[1m(\u001b[22m\u001b[22m::String\u001b[1m)\u001b[22m\u001b[22m at \u001b[1m/Users/bryan_luu30794/.julia/v0.6/IJulia/src/stdio.jl:130\u001b[22m\u001b[22m\n",
      " [7] \u001b[1m(::Base.##302#303{IJulia.#send_stdout,Timer})\u001b[22m\u001b[22m\u001b[1m(\u001b[22m\u001b[22m\u001b[1m)\u001b[22m\u001b[22m at \u001b[1m./event.jl:436\u001b[22m\u001b[22m\n",
      "while loading In[64], in expression starting on line 1\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "Dict{String,Any} with 13 entries:\n",
       "  \"flows\"     => 32×7 Named Array{Float64,2}…\n",
       "  \"cp\"        => 210\n",
       "  \"flowcaps\"  => 32×7 Named Array{Float64,2}…\n",
       "  \"revenue\"   => 2.59498e6\n",
       "  \"supply\"    => 12×7 Named Array{Float64,2}…\n",
       "  \"storage\"   => 12×7 Named Array{Float64,2}…\n",
       "  \"losses\"    => 32×7 Named Array{Float64,2}…\n",
       "  \"cycleterm\" => 1.10108e5\n",
       "  \"demand\"    => 12×7 Named Array{Float64,2}…\n",
       "  \"cycles?\"   => false\n",
       "  \"seed\"      => 603204\n",
       "  \"costs\"     => 1012.3\n",
       "  \"dirt\"      => 5340.32"
      ]
     },
     "execution_count": 64,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "data = solve_flows(price=1000, cost=1, penalty=10, cp=10, verbose=false,\n",
    "    cp_step=100, max_iter=10,\n",
    "    random_supply=true, random_demand=true, random_dist=:Uniform, random_std_p=0.5, no_sun=3, no_wind=2, seed=603204)\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "day = 1\n",
    "plot_flow(data[\"flows\"][:,day])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "scrolled": false
   },
   "outputs": [],
   "source": [
    "for day in days\n",
    "    println(\"====== Day \", day, \" ======\")\n",
    "    sleep(0.1)\n",
    "    display(plot_flow(data[\"flows\"][:, day]))\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "println(sum(data[\"supply\"], 1) - 10000)\n",
    "println(sum(data[\"demand\"], 1))"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.6.4",
   "language": "julia",
   "name": "julia-0.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "0.6.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
